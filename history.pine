//@version=3

// https://github.com/chingc/TradingView

study(title="Price Change History", shorttitle="Price Change")

src = input(close, title="Source")
since = input(1, title="Look Back", minval=1)  // the number of trading sessions to consider
above = input(5, title="Highlight Above", type=float, minval=0)  // use a different color for values above this
below = input(-5, title="Highlight Below", type=float, maxval=0)  // use a different color for values below this
diff = input(true, title="As Percentage") ? change(src, since) / offset(src, since) * 100 : change(src, since)  // show percentage change or dollar change
bars = input(50, title="% Highlighted Within", minval=1)  // the number of trading sessions considered to calculate how many of them are highlighted

paint(x) => x > above ? aqua : x < below ? fuchsia : x > 0 ? green : red

highlighted(x, y) =>
    highlighted = 0
    for i = 0 to y - 1
        if x[i] > above or x[i] < below
            highlighted := highlighted + 1
    highlighted / y

plot(diff, style=columns, color=paint(diff))
plot(highlighted(diff, bars), color=orange, transp=100)

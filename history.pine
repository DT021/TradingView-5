//@version=4

// https://github.com/chingc/TradingView

study(title="Price Change History", shorttitle="Price Change", precision=2)

src = input(close, title="Source")
sessions = input(1, title="Trading Sessions", minval=1)  // number of trading sessions to consider
upper = input(5, title="Highlight Above", type=input.float, minval=0)  // highlight values above this
lower = input(-5, title="Highlight Below", type=input.float, maxval=0)  // highlight values below this
range = input(50, title="Highlight Range", minval=1)  // number of trading sessions to consider to calculate percent highlighted

// show change data as a percent or decimal
dataset = if input(true, title="As Percentage")
    change(src, sessions) / offset(src, sessions) * 100
else
    change(src, sessions)

// aqua and fuchsia are highlights; green and red are normal
paint(x) => x > upper ? color.aqua : x < lower ? color.fuchsia : x > 0 ? color.green : color.red

// the number of sessions highlighted in a given range as a percent
highlighted(x) =>
    count = 0
    for i = 0 to range - 1
        if x[i] > upper or x[i] < lower
            count := count + 1
    count / range

// plot the change
plot(dataset, style=plot.style_columns, color=paint(dataset))

// plot the percent hightlighted
// this will show up as the orange number with a value between 0 and 1
// the graph itself isn't helpful and is hidden by having transp at 100
plot(highlighted(dataset), color=color.orange, transp=100)

//@version=3

// This is a modified version of the built-in TradingView Ichimoku Cloud.
// - Variables changed to their proper Japanese names.
// - The space between tenkan and kijun is filled.

study(title="Ichimoku Cloud", shorttitle="Ichimoku", overlay=true)

median(period) => avg(highest(period), lowest(period))

tenkan_sen_period = input(9, "Tenkan-sen", minval=1)
kijun_sen_period = input(26, "Kijun-sen", minval=1)
senkou_span_period = input(52, "Senkou Span", minval=1)
chikou_span_period = input(26, "Chikou Span", minval=1)

tenkan_sen = median(tenkan_sen_period)
kijun_sen = median(kijun_sen_period)
senkou_span_a = avg(tenkan_sen, kijun_sen)
senkou_span_b = median(senkou_span_period)

plot(close, "Chikou Span", orange, offset=-chikou_span_period)

pt = plot(tenkan_sen, "Tenkan-sen", aqua)
pk = plot(kijun_sen, "Kijun-sen", yellow)
pa = plot(senkou_span_a, "Senkou Span A", green, offset=chikou_span_period)
pb = plot(senkou_span_b, "Senkou Span B", red, offset=chikou_span_period)

fill(pt, pk, tenkan_sen > kijun_sen ? aqua : yellow, 80, "TK Cloud")
fill(pa, pb, senkou_span_a > senkou_span_b ? green : red, 80, "Senkou Cloud")
